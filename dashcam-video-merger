#!/usr/bin/env perl

use Class::Struct;
use Getopt::Tabular;
use File::Spec::Functions qw(catdir rel2abs);
use File::Temp;

use FindBin;
use lib "$FindBin::Bin/lib";
use Config::Sectioned;

use autodie;
use strict;
use warnings;


struct Config => [
    input_dir => '$',
    tmp_dir => '$',
    playlist => '$',
    output_file => '$',
    ffmpeg => 'FfmpegConfig',
];

struct FfmpegConfig => [
    filter_complex => '$',
    codec => '$',
    threads => '$',
    args => '$',
];

my $ffmpeg_codec = '';
# H265
#$ffmpeg_codec = '-c:v libx265 -x265-params log-level=warning';

exit main();


sub ffmpeg_join_cmd {
    my ($playlist, $out) = @_;
    return "ffmpeg -f concat -safe 0 -i $playlist -c copy $out";
}

sub ffmpeg_pip_cmd {
    my ($i1, $i2, $ffcfg, $out) = @_;
    return sprintf 'ffmpeg -i "%s" -i "%s" %s -threads %d -filter_complex "%s" %s "%s"',
        $i1, $i2,
        $ffcfg->codec,
        $ffcfg->threads,
        $ffcfg->filter_complex,
        $ffcfg->args,
        $out;
}

sub run_command {
    my $cmd = shift;
    print "$cmd\n";
    system($cmd) == 0 or die "Comand failed with code: $?";
}

sub overlay_back_on_front {
    my ($out_dir, $ffcfg, @front_chunk_files) = @_;

    @front_chunk_files > 0 or die "No video chunks found!";

    my @out_chunks;
    while (my ($index, $front) = each @front_chunk_files) {
        $front =~ m,Front/(\S*)F\.MP4$,
            or die "Unexpected filename format: $front";

        my $base = $1;
        my $back = "Back/${base}B.MP4";
        my $output = "$out_dir/${base}.MP4";

        -f $front or die "File '$front' is missing";
        -f $back or die "File '$back' is missing";
        -e $output and die "File '$output' already exists";

        printf "Chunk %d/%d:\n", map { $_ + 1 } $index, $#front_chunk_files;
        run_command(ffmpeg_pip_cmd $front, $back, $ffcfg, $output);
        push @out_chunks, "${base}.MP4";
    }
    return @out_chunks;
}

sub write_playlist {
    my ($playlist, @outs) = @_;

    open my $fh, '>', $playlist;
    print $fh join "\n", map { "file '$_'" } @outs;
}

sub main {
    my $input_dir = '.';
    my $output_file = 'output.mp4';
    my $profileName = 'default';
    my $threads = 0;

    my @options = (
        ['-i', 'string', 1, \$input_dir, "Path where Front/ and Back/ with video files are located" ],
        ['-o', 'string', 1, \$output_file, "Name of output file" ],
        ['--profile', 'string', 1, \$profileName, "Name of profile to use for encoding" ],
        ['--threads', 'integer', 1, \$threads, "Number of ffmpeg threads used for encoding (0 means let ffmpeg decide)" ],
    );
    Getopt::Tabular::SetOptionPatterns qw|(--)([\w-]+) (-)(\w+)|;
    Getopt::Tabular::SetHelpOption("--help");
    GetOptions(\@options, \@ARGV) or exit(1);

    my $profile = Config::Sectioned::read_file("$FindBin::Bin/$profileName.profile");

    my $tmp_dir = File::Temp->newdir("dvmXXXXXX", TMPDIR => 1);
    print "Will use directory $tmp_dir to store temporary files...\n";

    my $config = Config->new(
        input_dir => (rel2abs $input_dir),
        output_file => (rel2abs $output_file),
        tmp_dir => $tmp_dir->dirname,
        ffmpeg => FfmpegConfig->new(
            filter_complex => $profile->{filter_complex},
            codec => $ffmpeg_codec,
            args => $profile->{ffmpeg_args},
            threads => $threads,
        ),
    );
    $config->playlist(catdir($config->tmp_dir, "playlist.txt"));

    chdir $config->input_dir;

    -d 'Front' && -d 'Back' or die
       "'Front' and 'Back' directories not found at @{[ $config->input_dir ]}\n"
       . "You probably need to set a different input path (or cd there).\n";

    my @outs = overlay_back_on_front($config->tmp_dir, $config->ffmpeg, sort glob 'Front/*.MP4');
    write_playlist $config->playlist, @outs;

    run_command(ffmpeg_join_cmd($config->playlist, $config->output_file));

    return 0;
}

